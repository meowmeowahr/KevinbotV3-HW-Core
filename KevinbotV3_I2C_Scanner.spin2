'' =================================================================================================
''
''   File....... KevinbotV3_I2C_Scanner.spin2
''   Purpose.... I2C bus scanner; shows connected devices in matrix format
''   Author..... Jon "JonnyMac" McPhalen with changes for Kevinbot by Kevin Ahr
''               Copyright (C) 2018-2021 Jon McPhalen, 2024 Kevin Ahr
''               -- see below for terms of use
''   E-mail..... jon.mcphalen@gmail.com, meowmeowahr@gmail.com
''   Started....
''   Updated.... 30 JUN 2024
''
''   {$P2}
''
'' =================================================================================================


con { timing }

  CLK_FREQ = 200_000_000                                        ' system freq as a constant
  MS_001   = CLK_FREQ / 1_000                                   ' ticks in 1ms
  US_001   = CLK_FREQ / 1_000_000                               ' ticks in 1us

  _clkfreq = CLK_FREQ                                           ' set system clock


con { terminal }

  BR_TERM  = 230_400                                            ' terminal baud rate

  #0, T_PST, T_ANSI                                             ' terminal types

  T_TYPE = T_ANSI


con { fixed io pins }

  RX1     = 63  { I }                                           ' programming / debug
  TX1     = 62  { O }

  SF_CS   = 61  { O }                                           ' serial flash
  SF_SCK  = 60  { O }
  SF_SDO  = 59  { O }
  SF_SDI  = 58  { I }

  SD_SCK  = 61  { O }                                           ' sd card
  SD_CS   = 60  { O }
  SD_SDI  = 59  { O }
  SD_SDO  = 58  { I }

  LED2    = 57  { O }                                           ' Eval and Edge LEDs
  LED1    = 56  { O }


con { app io pins }

  SDA     =  41  { IO }                                          ' I2C bus
  SCL     =  42  { O }


con

  #true,  ON, OFF                                               ' digital control
  #false, NO, YES

  TRIES = 1                                                     ' should not be needed!


obj

' main                                                          ' * master Spin cog
  i2c  : "jm_i2c"                                               '   i2c bus driver
  term : "jm_fullduplexserial"                                  ' * serial IO for terminal
  ansi : "jm_ansi"                                              '   ANSI terminal control sequences

' * uses cog when loaded


dat

  Banner1       byte    "Kevinbot I2C Device Scanner", 0
  Banner2       byte    "-- dddd_aaa (7-bit) format", 0

  Header        byte    "     0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F", 13, 10
                byte    0


var

  byte  found[32]                                               ' list of found devices


pub main() | count, type, addr, devid, ok

  setup()

  wait_for_terminal(true)

  if (T_TYPE == T_PST)
    term.fstr3(string("%s\r\n%s\r\n\r\n%s"), @Banner1, @Banner2, @Header)
    term.str(string("!!! PST TERMINAL SUPPORT IS DEPRECATED AND WILL BE DROPPED IN THE FUTURE !!!\r\n"))
  else
    term.fstr3(string("%s%s%s\r\n"), ansi.fgnd(ansi.BR_CYAN), @Banner1, ansi.normal())
    term.fstr2(string("%s\r\n\r\n%s"), @Banner2, @Header)

  count := 0

  repeat type from $1 to $7                               ' %1111 not valid
    term.hex(type << 4)
    term.txn(" ", 2)
    repeat addr from $0 to $F
      devid := (type << 5) | (addr << 1)                        ' build device id
      if (i2c.present(devid))                                   ' ping device
        if (T_TYPE == T_ANSI)
          term.str(ansi.bold())
          term.str(ansi.fgnd(ansi.BR_GREEN))
        term.hex(devid >> 1)                                     ' show 8-bit id as hex
        if (T_TYPE == T_ANSI)
          term.str(ansi.normal())
        term.tx(" ")
        found[count++] := devid                                 ' add to found list
      else
        term.str(string("-- "))
      waitms(1)
    term.tx(13)
    term.tx(10)

  if (T_TYPE == T_PST)
    term.fstr1(string("\rDevices: %d\r\n"), count)
  else
    term.fstr3(string("\rDevices: %s%d%s\r\n"), ansi.fgnd(ansi.BR_BLUE), count, ansi.normal())


  ' show found devices details

  if (count)
    repeat addr from 0 to count-1
      devid := found[addr]
      term.fstr3(string("-- 8-bit $%.2x \%%.4b_%.3b_0"), devid, devid.[7..4], devid.[3..1])
      term.fstr3(string(", 7-bit $%.2x \%%.4b_%.3b\r\n"), devid >> 1, devid.[7..4], devid.[3..1])

  repeat
    waitct(0)


pub setup()

  i2c.setup(SCL, SDA, 50, i2c.PU_15K)                          ' i2c @ 100kHz, external pull-ups

  term.tstart(BR_TERM)                                           ' start terminal io


pub wait_for_terminal(clear)

  ' Download to RAM with F10
  ' F12 to open PST
  ' Click [Enable]
  ' Press Enter

  term.rxflush()
  term.rx()
  if (clear)
    if (T_TYPE == T_PST)
      term.tx(term.CLS)
    else
      term.str(ansi.hide_cursor())
      term.str(ansi.home())
      term.str(ansi.cls())


con { license }

{{

  Terms of Use: MIT License

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

}}